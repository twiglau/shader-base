# 引擎阴影计算的原理:  
* 阴影;
> 阴影的效果, 只会影响接收物体的颜色, 不会影响投射物体的颜色; 
* 判断;
> 要计算阴影, 着色我们接收的物体的表面片元的时候, 要判断一下, 是否叠加阴影? 
> 判断依据: 我们的片元着色点是否在阴影范围内, 如果在, 我们就叠加阴影, 如果不再我们就不用叠加阴影颜色;

* 如何判断一个片元是否在阴影范围内?  
1. 在我们的光源的位置与方向, 摆放一台 `摄像机`, 从`摄像机`角度, 来 `绘制`整个`场景`; `场景`中的物体是哪些可以投射阴影的物体, 如何哪些物体不可以投射阴影, 那么这个物体相当于不再 `摄像机`的视野范围内, 相当于不可见;
2. 绘制我们的场景的阴影数据; => 调用物体的一个 `特殊的Pass`,不是把颜色绘制到屏幕, 而是把我们的每个`可见点`的`深度信息`绘制出来;
3. 我们真正绘制游戏场景的时候, 我们就调用Shader来绘制;
> 需要编写Shader来完成;
> 如何区分:阴影内,和外的点; => 找到片元点的位置 => 转到光源为坐标系下的坐标, [深度信息];
> 根据深度信息, 我们就可以判断, 如果这个 `深度信息`, 是 <= 我们的阴影的深度的, 那么我们就是阴影外;
> 如果深度信息, 如果 > 阴影深度, 表示这个点 需要节点阴影, 这样在着色的时候计算阴影就可以了.

4. 计算: 
> 阴影投射: 专门编写, 阴影投射的 pass, 来计算阴影的深度信息; 
> 阴影接收: 专门编写代码, 把颜色叠加阴影;




# 阴影投射Pass的编写 `builtin-standard.effect` `general-vs.chunk`
1. 在Shader里面定义一个阴影投射的 pass, 所以计算阴影的时候,就会走这个 pass;
> 标记一个pass, 为阴影投射的 pass `phase: shadow-caster`

2. 计算出来每个片元的深度信息;

```
CCEffect %{
  techniques:
  - name: opaque
    passes:
    - vert: general-vs:vert # builtin header
      frag: unlit-fs:frag
      properties: &props
        mainTexture:    { value: white }
        mainColor:      { value: [1, 1, 1, 1], editor: { type: color } }

    - &shadow-caster
      vert: shadow-caster-vs:vert
      frag: shadow-caster-fs:frag
      phase: shadow-caster
      rasterizerState:
        cullMode: front
  - name: transparent
    passes:
    - vert: general-vs:vert # builtin header
      frag: unlit-fs:frag
      blendState:
        targets:
        - blend: true
          blendSrc: src_alpha
          blendDst: one_minus_src_alpha
          blendSrcAlpha: src_alpha
          blendDstAlpha: one_minus_src_alpha
      properties: *props
}%

CCProgram unlit-fs %{
  precision highp float;
  #include <output>
  #include <cc-fog-fs>
  #include <cc-shadow-map-base>

  in vec2 v_uv;
  in vec3 v_normal;
  in vec4 v_shadowPos; // 着色时候片元点，相对于光源投影的阴影坐标;

  uniform sampler2D mainTexture;

  uniform Constant {
    vec4 mainColor;
  };

  vec4 frag () {
    vec4 col = mainColor * texture(mainTexture, v_uv);

    vec3 N = normalize(v_normal);
    vec3 L = normalize(-cc_mainLitDir.xyz);
    float NL = max(dot(N, L), 0.001);

    CC_SHADOW_FACTOR_BASE(col, NL, v_shadowPos, L, N);// 
    CC_APPLY_FOG(col);
    return CCFragOutput(col);
  }
}%

CCProgram shadow-caster-vs %{
  precision highp float;
  #include <input-standard>
  #include <cc-local-batch>

  #include <cc-shadow>


  out vec2 v_uv;
  out vec2 v_uv1;
  out vec4 v_worldPos;

  out float v_clip_depth; // 每个顶点的深度信息;

  vec4 vert () {

    mat4 matWorld, matWorldIT;
    CCGetWorldMatrixFull(matWorld, matWorldIT);

    v_worldPos = matWorld * vec4(a_position, 1); // 世界坐标
    vec4 clipPos = cc_matLightViewProj * v_worldPos; // 以光源的位置 来投射;

    v_clip_depth = clipPos.z / clipPos.w * 0.5 + 0.5; // 深度信息;

    return clipPos;
  }
}%

CCProgram shadow-caster-fs %{
  precision highp float;
  #include <packing>
  #include <cc-shadow>

  in vec2 v_uv;
  in vec2 v_uv1;
  in vec4 v_worldPos;
  in float v_clip_depth; // 每个片元的深度信息

  vec4 frag () {
    
    return vec4(v_clip_depth, 1.0, 1.0, 1.0);
  }
}%
```  

# 